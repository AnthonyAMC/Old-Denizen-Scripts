################################################################################
#
#  Master Waitress Assignment Script
#
# The Waitress will greet players, deliver menus, take orders, and deliver food.
#
MasterWaitressNPC:
  type: assignment
  speed: 1t
  debug: true

  actions:
    on assignment:
      - run locally assignment instantly
    on remove assignment:
      - run locally unassign instantly
    on spawn:
      - run locally spawn instantly delay:1t
    on despawn:
      - run locally despawn instantly
    on click:
      - run locally click_<npc.flag[status]||drunk> instantly
    on move proximity:
      - run locally proximity_<npc.flag[status]||drunk> instantly


  assignment:
  # What happens on assignment. We want this as a subscript so we can run it
  # outside the on assignment action if needed.
    - trigger name:chat state:true radius:5 cooldown:5s
    - trigger name:click state:true radius:8 cooldown:5s
    - trigger name:proximity state:true radius:3 cooldown:8s
    - trigger name:damage state:true radius:5 cooldown:5s
    - vulnerable state:false
    - lookclose true range:5 realistic
    - flag npc respawn_location:<npc.location>
    - inject s@MasterWaitressNPC p:findRestaurant instantly
    - define wait '5'
    - define radius '15'
    - define speed '0.8'
    - define location '<npc.location>'
    - define height '1'
    - define materials '<npc.location.standing_on.material.name>'
    - flag npc Pathfind:!
    - flag npc dRestaurant_FidgetValues:!
    - flag npc dRestaurant_FidgetValues:|:%restaurant%|%wait%|%radius%|%speed%|%location%|%height%|%materials%
    - flag npc dRestaurant_MenuQueue:!
    - flag npc TicketQueue:!
    - inject locally spawn instantly

  unassign:
    - flag npc respawn_location:!
    - flag npc Pathfind:!
    - flag npc dRestaurant_FidgetValues:!
    - flag npc dRestaurant_MenuQueue:!
    - flag npc TicketQueue:!
    - despawn

  spawn:
    - narrate format:GenericNPCFormat "Alright, time to start work..." targets:<npc.location.find.players.within[5]||null>
    - if <queue.exists[idleQueue_Waitress_<npc.id>]||false> queue queue:idleQueue_Waitress_<npc.id> stop
    - if <queue.exists[processTicket_<npc.id>]||false> queue queue:processTicket_<npc.id> stop
    - if <queue.exists[collectMenu_<npc.id>]||false> queue queue:collectMenu_<npc.id> stop
    - if <npc.navigator.target_type.is[==].to[entity]||false> follow stop target:<npc.navigator.target_entity||null>
    - run s@MasterWaitressNPC p:processTicketQueue id:processTicket_<npc.id> delay:2s

  despawn:
    - if <queue.exists[idleQueue_Waitress_<npc.id>]||false> queue queue:idleQueue_Waitress_<npc.id> stop
    - if <queue.exists[processTicket_<npc.id>]||false> queue queue:processTicket_<npc.id> stop
    - if <queue.exists[collectMenu_<npc.id>]||false> queue queue:collectMenu_<npc.id> stop
    - if <npc.navigator.target_type.is[==].to[entity]||false> follow stop target:<npc.navigator.target_entity||null>
    - narrate format:GenericNPCFormat "Phew... I<&sq>m glad to be done work for the day!" targets:<npc.location.find.players.within[5]||null>

  damage:
  # Just a code stub
    - narrate format:GenericNPCFormat "Why would you do that?" targets:<npc.location.find.players.within[5]||null>

#
#--------------------------------------
#
#  Logic Utilities
#
#
  collectMenus:
  # Get the menu from the first player in the menu queue.
  # Should add nullChecks for this
    - ^if !<npc.is_spawned||false> queue clear
    - ^flag npc Status:order
    - ^define fidgetValues '<npc.flag[dRestaurant_FidgetValues].as_list||null>'
    - ^define restaurant '<def[fidgetValues].get[1]||null>'
    - ^define player '<npc.flag[dRestaurant_MenuQueue].as_list.get[1].as_player||null>'
    - ^if <def[player].is[!=].to[null]||false> {
      - if <def[player].is_online>
        && <def[player].location.is_within[cu@restaurant_%restaurant%]||false> {
        - follow target:%player%
        - inject s@dRestaurant p:followPlayer_waitress
        - follow stop target:%player%
        - look <def[player].eye_location> d:2s
        - wait 10t
        - narrate format:GenericNPCFormat "Your order should be out in just a moment." targets:%player%
        - animate <npc> animation:arm_swing
        - playsound <def[player].location> sound:silverfish_walk pitch:0.3
        - playsound <def[player].location> sound:step_grass pitch:0.7
        }
      - inventory set 'd:<def[player].inventory>' 'o:i@dRestaurant_Menu' 'slot:<def[player].inventory.find_imperfect[drestaurant_menu]>'
      - take 'i@dRestaurant_Menu' player:%player%
      - flag %player% dRestaurant_Order:!
      - flag <npc> dRestaurant_MenuQueue:<-:%player%
      - wait 10t
      - if !<npc.flag[TicketQueue].as_list.is_empty||true> {
        - run s@MasterWaitressNPC p:processTicketQueue id:processTicket_<npc.id> delay:2t
        - queue clear
        }
      - if !<npc.flag[dRestaurant_MenuQueue].as_list.is_empty||true> {
        - inject s@MasterWaitressNPC p:collectMenus
        }
      }
    - ^if !<npc.flag[TicketQueue].as_list.is_empty||true> {
      - run s@MasterWaitressNPC p:processTicketQueue id:processTicket_<npc.id> delay:2t
      - queue clear
      }
    - ^if <queue.exists[idleQueue_Waitress_<npc.id>]||false> queue queue:idleQueue_Waitress_<npc.id> stop
    - ^run s@MasterWaitressNPC p:idleQueue_Waitress id:idleQueue_Waitress_<npc.id> delay:2t
    - ^queue clear

  processTicketQueue:
  # Run through a list of orders waiting to be delivered. Rinse and repeat until
  # the queue is cleared.
  # Should add nullChecks for this
    - ^if !<npc.is_spawned||false> queue clear
    - ^flag npc Status:deliver
    - ^define fidgetValues '<npc.flag[dRestaurant_FidgetValues].as_list||null>'
    - ^define restaurant '<def[fidgetValues].get[1]||null>'
    - ^define TicketQueueEntry '<npc.flag[TicketQueue].as_list.get[1]||null>'
    - ^if <def[TicketQueueEntry].is[!=].to[null]||false> {
      - define ticketNumber '<def[TicketQueueEntry].split_by[/].get[1]>'
      - define displayItems '<def[TicketQueueEntry].split_by[/].get[2].split_by[\].as_list>'
      - define tickets '<server.flag[dRestaurantTickets].as_list>'
      - define ticket '<def[tickets].get[<def[tickets].get_sub_items[1].split_by[/].find[%ticketNumber%]>]>'
      - define player '<def[ticket].split_by[/].get[4].as_player>'
      - define foodItems '<def[ticket].split_by[/].get[5].as_list>'
      - define speed '<def[fidgetValues].get[4]||0.9>'
      - inject s@dRestaurant p:deliverFood_waitress
      - flag npc TicketQueue:<-:%TicketQueueEntry%
      - flag server dRestaurantTickets:<-:%ticket%
      - wait 5t
      - if !<npc.flag[dRestaurant_MenuQueue].as_list.is_empty||true> {
        - run s@MasterWaitressNPC p:collectMenus id:collectMenu_<npc.id> delay:2t
        - queue clear
        }
      - if !<npc.flag[TicketQueue].as_list.is_empty||true> {
        - inject s@MasterWaitressNPC p:processTicketQueue
        }
      }
    - ^if !<npc.flag[dRestaurant_MenuQueue].as_list.is_empty||true> {
      - run s@MasterWaitressNPC p:collectMenus id:collectMenu_<npc.id> delay:2t
      - queue clear
      }
    - ^if <queue.exists[idleQueue_Waitress_<npc.id>]||false> queue queue:idleQueue_Waitress_<npc.id> stop
    - ^run s@MasterWaitressNPC p:idleQueue_Waitress id:idleQueue_Waitress_<npc.id> delay:2t
    - ^queue clear

  idleQueue_Waitress:
    - ^if !<npc.is_spawned||false> queue clear
    - ^if <queue.exists[processTicket_<npc.id>]||false> queue queue:processTicket_<npc.id> stop
    - ^if <queue.exists[collectMenu_<npc.id>]||false> queue queue:collectMenu_<npc.id> stop
    - ^flag npc Status:idle
    - ^lookclose state:true
    - ^define fidgetValues '<npc.flag[dRestaurant_FidgetValues].as_list||null>'
    - ^define list li@100/Fidget
    - ^inject s@WeightedRandomChoice p:asDef instantly
    - ^define restaurant '<def[fidgetValues].get[1]||null>'
    - ^define range '<def[fidgetValues].get[3]||30>'
    - ^define speed '<def[fidgetValues].get[4]||0.7>'
    - ^inject s@dRestaurant idleQueue_Waitress_%result%
    - ^define wait '<def[fidgetValues].get[2]||10>'
    - ^wait <util.random.int[<def[wait].mul[0.5].as_int>].to[<def[wait].mul[1.5].as_int>]>s
    - ^run s@MasterWaitressNPC p:processTicketQueue id:processOrder_<npc.id> delay:2t
    - ^queue clear


  findRestaurant:
  # This requires an NPC object and should only be run when the player is
  # getting a menu from a waitress in the on click action
    - define restaurants li@
    - foreach <npc.location.cuboids||li@> {
      - if <def[value].notable_name.split[_].get[1].is[==].to[Restaurant]||false> {
        - define restaurant '<def[value].notable_name.split[_].get[2]>'
        - define owner '<server.flag[dRestaurant.%restaurant%.Owner].as_player>'
        - if <def[owner].is[==].to[<npc.owner>]> {
          - define restaurants '<def[restaurants].include[%restaurant%]>'
          }
        }
      }
    # Because why else would any sane admin have restaurant cuboids overlap?
    - define restaurant '<def[restaurants].random||null>'
    - if <def[restaurant].is[==].to[null]> {
      - narrate format:GenericNPCFormat "Oh dear, I seem to be quite lost..." targets:<npc.location.find.players.within[5]||null>
      - flag npc status:drunk
      - queue clear
      }

  assignWaitress:
  # This assumes we know the restaurant
    - define waitresses li@
    - define owner '<server.flag[dRestaurant.%restaurant%.Owner].as_player>'
    - foreach <cu@restaurant_%restaurant%.list_npcs||li@> {
      - define npc %value%
      - if <def[owner].is[==].to[<def[npc].owner>]||false>
        && <def[npc].script.name.is[==].to[MasterWaitressNPC]||false> {
        - define waitresses '<def[waitresses].include[%npc%]>'
        }
      }
    # Now we have a list of waitresses, let's find the least busy
    - foreach <def[waitresses]||li@> {
      - define waitress '<def[waitress]||%value%>'
      - if <def[value].flag[TicketQueue].size.is[LESS].than[<def[waitress].flag[TicketQueue].size>]||false> {
        - define waitress %value%
        }
      }

  assignChef:
  # This assumes we know the restaurant
    - define chefs li@
    - define owner '<server.flag[dRestaurant.%restaurant%.Owner].as_player>'
    - foreach <cu@restaurant_%restaurant%.list_npcs||li@> {
      - define npc %value%
      - if <def[owner].is[==].to[<def[npc].owner>]||false>
        && <def[npc].script.name.is[==].to[MasterChefNPC]||false> {
        - define chefs '<def[chefs].include[%npc%]>'
        }
      }
    # Now we have a list of chefs, let's find the least busy
    - foreach <def[chefs]||li@> {
      - define chef '<def[chef]||%value%>'
      - if <def[value].flag[OrderQueue].size.is[LESS].than[<def[chef].flag[OrderQueue].size>]||false> {
        - define chef %value%
        }
      }

  assignTicketNumber:
    - repeat 20 {
      - define ticketNumber <util.random.duuid>
      - define ticketList <server.flag[dRestaurantTickets].get_sub_items[1].split_by[/]||null>
      - if !<def[ticketList].contains[%ticketNumber%]> repeat stop
      - if <def[value].is[OR_MORE].than[20]> {
        - define ticketNumber "LastLostTicketDetails"
        - announce "<&b>dRestaurant<&co> <&c>Order Lost - Couldn<&sq>t get a valid ticket number!" to_console
        - announce "<&b>dRestaurant<&co> <&c>%ticketNumber%" to_console
        - announce "<&b>dRestaurant<&co> <&c>Chef<&co> %chef%" to_console
        - announce "<&b>dRestaurant<&co> <&c>Waitress<&co> %waitress%" to_console
        - announce "<&b>dRestaurant<&co> <&c>Player<&co> <player>" to_console
        - announce "<&b>dRestaurant<&co> <&c>foodItems<&co> %foodItems%" to_console
        }
      }

#
#  END Logic Utilities
#
#--------------------------------------
#
#  MasterWaitress Status Actions
#
# We're using an NPC status system for action responses. This is a low latency
# and low load method that allows for very fast NPC reactions since they do not
# have to do ANY comparisons. They just run the correct subscript based on the
# action and their current status.
#
#
#--------------------------------------
#  Click Responses
#
  click_drunk:
  # A catchall if something derps
    - random {
      - narrate format:GenericNPCFormat "I quit!" targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I<&sq>m done with this place!" targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I should have called out!" targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I had way too much to drink last night..." targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I<&sq>m not feeling well... I have to go." targets:<npc.location.find.players.within[5]||null>
      }
    - wait 2s
    - despawn <npc>

  click_idle:
  # When the Waitress is available for whatever
    - if <player.flag[dRestaurant_Order].split[/].get[3].is[==].to[true]||false> {
      - narrate format:GenericNPCFormat "Someone will collect your menu soon."
      - queue clear
      }
    - if <player.has_flag[dRestaurant_Order]> {
      - narrate format:GenericNPCFormat "Open up your menu and complete your order."
      - queue clear
      }
    - flag npc status:order
    - if <queue.exists[idleQueue_Waitress_<npc.id>]||false> queue queue:idleQueue_Waitress_<npc.id> stop
#    - inject locally findRestaurant instantly
    - define fidgetValues '<npc.flag[dRestaurant_FidgetValues].as_list||null>'
    - define restaurant '<def[fidgetValues].get[1]||null>'
    - if <def[restaurant].is[==].to[null]> {
      - flag npc Status:drunk
      - queue clear
      }
    - flag <player> dRestaurant_Order:%restaurant%
    - define speed '<def[fidgetValues].get[4].add[0.1].as_money||0.8>'
    - define pathMaterial '<def[fidgetValues].get[7].split_by[/]||cobblestone|coal_block|cobblestone_stairs>'
    - define loc <player.location.find.surface_blocks[%pathMaterial%].within[3].get[1]||<npc.location>>
    - ~walk auto_range %loc% 'speed:%speed%'
    - look <player.eye_location> d:2s
    - wait 5t
    - random {
      - narrate format:GenericNPCFormat "Click on the items in the menu." targets:<player>
      - narrate format:GenericNPCFormat "Browse through the menu. I<&sq>m sure you<&sq>ll find something you like." targets:<player>
      - narrate format:GenericNPCFormat "We serve only the best food prepared by Master Chefs." targets:<player>
      - narrate format:GenericNPCFormat "Open the menu and click on the items you want." targets:<player>
      - narrate format:GenericNPCFormat "Here you go. Take a look through our menu and choose what you would like." targets:<player>
      }
    - animate <npc> animation:arm_swing
    - give 'i@dRestaurant_Menu[display_name=%restaurant% Menu]'
    - wait 15t
    - random {
      - narrate format:GenericNPCFormat "Place your order and I<&sq>ll come get the menu." targets:<player>
      - narrate format:GenericNPCFormat "Click the Order Accept button to place your order." targets:<player>
      - narrate format:GenericNPCFormat "A waitress will take the menu when you<&sq>re done." targets:<player>
      - narrate format:GenericNPCFormat "Click the Order Accept button when you<&sq>re finished." targets:<player>
      - narrate format:GenericNPCFormat "Someone will come grab the menu when you are ready." targets:<player>
      }
    - wait 30t
    - if <queue.exists[processTicket_<npc.id>]||false> queue queue:processTicket_<npc.id> stop
    - run s@MasterWaitressNPC p:processTicketQueue id:processTicket_<npc.id>

  click_order:
  # When the Waitress is taking an order
    - wait 15t
    - random {
      - narrate format:GenericNPCFormat "I<&sq>ll be right with you <player.name>." targets:<player>
      - narrate format:GenericNPCFormat "I<&sq>m with another customer right now." targets:<player>
      - narrate format:GenericNPCFormat "One moment please, I<&sq>m taking an order." targets:<player>
      - narrate format:GenericNPCFormat "Someone will be with you soon." targets:<player>
      - narrate format:GenericNPCFormat "Hold on a sec there dear, I<&sq>m taking another order right now." targets:<player>
      }

  click_deliver:
  # When the Waitress is delivering food
    - wait 15t
    - random {
      - narrate format:GenericNPCFormat "We<&sq>re a little busy right now <player.name>." targets:<player>
      - narrate format:GenericNPCFormat "I<&sq>m running food right now <player.name>." targets:<player>
      - narrate format:GenericNPCFormat "My hands are a little full <player.name>" targets:<player>
      - narrate format:GenericNPCFormat "<player.name> they really don<&sq>t pay me enough for this..." targets:<player>
      - narrate format:GenericNPCFormat "I<&sq>m pretty tired <player.name>." targets:<player>
      - narrate format:GenericNPCFormat "Please have a seat and a Waitress will be right with you." targets:<player>
      }

#
#--------------------------------------
#  Order Up Responses
#
  orderup_drunk:
  # A catchall if something derps
    - random {
      - narrate format:GenericNPCFormat "I quit!" targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I<&sq>m done with this place!" targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I should have called out!" targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I had way too much to drink last night..." targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I<&sq>m not feeling well... I have to go." targets:<npc.location.find.players.within[5]||null>
      }
    - wait 2s
    - despawn <npc>

  orderup_idle:
  # When the Waitress is available for whatever
    - if <util.random.int[1].to[100].is[OR_MORE].than[98]> {
      - random {
        - narrate format:GenericNPCFormat "Ticket %ticketNumber% heard!" targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "Oh, another order is ready." targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "Yay! Food to run!" targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "I<&sq>m a happy little worker bee." targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "Time to get working." targets:<npc.location.find.players.within[5]||null>
        }
      }

  orderup_order:
  # When the Waitress is taking an order
    - if <util.random.int[1].to[100].is[OR_MORE].than[98]> {
      - random {
        - narrate format:GenericNPCFormat "Heard ticket %ticketNumber%! I<&sq>m taking an order right now." targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "Buzz buzz." targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "The kitchen sure is busy!" targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "They are working hard back there." targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "The guy on the grill is by bf." targets:<npc.location.find.players.within[5]||null>
        }
      }

  orderup_deliver:
  # When the Waitress is delivering food
    - if <util.random.int[1].to[100].is[OR_MORE].than[98]> {
      - random {
        - narrate format:GenericNPCFormat "Heard ticket %ticketNumber%! I<&sq>m running food right now." targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "My goodness, the food just keeps on coming!" targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "I<&sq>m in the weeds!" targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "I<&sq>m going as fast as I can!" targets:<npc.location.find.players.within[5]||null>
        - narrate format:GenericNPCFormat "This is a rough night!" targets:<npc.location.find.players.within[5]||null>
        }
      }
#
#--------------------------------------
#  Proximity Responses
#
  proximity_drunk:
  # A catchall if something derps
    - random {
      - narrate format:GenericNPCFormat "I quit!" targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I<&sq>m done with this place!" targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I should have called out!" targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I had way too much to drink last night..." targets:<npc.location.find.players.within[5]||null>
      - narrate format:GenericNPCFormat "I<&sq>m not feeling well... I have to go." targets:<npc.location.find.players.within[5]||null>
      }
    - wait 2s
    - despawn <npc>

  proximity_idle:
  # When the Waitress is available for whatever
    - if <util.random.int[1].to[100].is[OR_MORE].than[99]> {
      - random {
        - narrate format:GenericNPCFormat "We<&sq>ve got the best food!" targets:<player>
        - narrate format:GenericNPCFormat "Our chefs were trained by the finest programmers!" targets:<player>
        - narrate format:GenericNPCFormat "I hope you enjoy your meal." targets:<player>
        - narrate format:GenericNPCFormat "You can watch the Chefs do what they do best!" targets:<player>
        - narrate format:GenericNPCFormat "Enjoy your stay!" targets:<player>
        }
      }

  proximity_order:
  # When the Waitress is taking an order
    - if <util.random.int[1].to[100].is[OR_MORE].than[99]> {
      - random {
        - narrate format:GenericNPCFormat "More tickets coming in!" targets:<player>
        - narrate format:GenericNPCFormat "Buzz buzz." targets:<player>
        - narrate format:GenericNPCFormat "The kitchen sure is busy!" targets:<player>
        - narrate format:GenericNPCFormat "They are working hard back there." targets:<player>
        - narrate format:GenericNPCFormat "The guy on the grill is by bf." targets:<player>
        }
      }

  proximity_deliver:
  # When the Waitress is delivering food
    - if <util.random.int[1].to[100].is[OR_MORE].than[99]> {
      - define ticketNumber '<npc.flag[TicketQueue].as_list.get[1].split_by[/].get[1]||null>'
      - if <def[ticketNumber].is[==].to[null]> queue clear
      - define player '<server.flag[dRestaurantTickets].as_list.map_get[%ticketNumber%].split[/].get[3].name>'
      - define food '<server.flag[dRestaurantTickets].as_list.map_get[%ticketNumber%].split[/].get[4].display>'
      - random {
        - narrate format:GenericNPCFormat "Where is %player%?" targets:<player>
        - narrate format:GenericNPCFormat "Have you seen %player%?" targets:<player>
        - narrate format:GenericNPCFormat "I<&sq>m looking for %player%." targets:<player>
        - narrate format:GenericNPCFormat "Hot %food% for %player%!" targets:<player>
        - narrate format:GenericNPCFormat "This %food% is for %player%!" targets:<player>
        }
      }

#
#  END MasterWaitress Status Actions
#--------------------------------------
#
#
#  END MASTERWAITRESS ASSIGNMENT SCRIPT CONTAINER
#
