################################################################################
#
#                            d W o r l d E d i t o r
#
#
#   Authors: |Anthony|, mcmonkey
#   Version: 0.22
#   dScript Version: 0.9.8-DEV-b535
#
#
#
#--- About this script
#
#  This is a basic recreation of WorldEdit in denizen. The wand and selection
#  tools are very useable. The command system is fairly robust, lacking only
#  TAB Completion. Permission system is in place. Editing tools are severly
#  lacking still.
#
#--- Permission nodes
#  dwe.admin:  All commands. Same as OP
#  dwe.chunk:  Set the chunk you are standing in as your selection
#  dwe.contract:  Contract your region selection
#  dwe.expand:  Expand your region selection
#  dwe.inset:  Inset your region selection
#  dwe.outset:  Outset your region selection
#  dwe.pos:  Use commands to set selection positions
#  dwe.set:  Set the blocks in a selection
#  dwe.shift:  Shift your selection
#  dwe.show:  Show your current region selection
#  dwe.wand:  Get the region selection wand
#  dwe.undo:  Undo previous edits
#
#
# TODO:
#  - TAB Completion
#  - Editing & Clipboard
#    - Copy
#    - Paste
#    - Stack
#    - Rotate
#  - Permissions
#    - Permissions for each individual command
#    - Parent permissions for categories (selection,tool,navigation)
#  - Config File
#    - Maxblocks to change per operation
#    - Undo history size
#    - async-blockedit
#
#
################################################################################
#
#  dWorldEditor Events
#
#  This should cover all dWE related world events.
#
dWE_Events:
  type: world
  debug: false
  events:
    on player clicks block:
      - if <c.item.scriptname.split[_].get[1].is[!=].to[dWE]||true> queue clear
      - determine passively CANCELLED
      - inject <c.item.scriptname.as_script> instantly

#
#  END dWorldEditor Events
#--------------------------------------
#
#
################################################################################
#
#  dWorldEditor Messages
#
# This is the house for various output messages
#
#
#--------------------------------------
#
dWE_Msg:
  type: item
  debug: false
  material: i@human_skull
  display name: "<&4>     [<&6>dWE<&4>]"
  lore:
  - <&5>Click for Help
  script:
    - ^define text '<&4>[<&6>dWE<&4>]'
    - ^define hover '{<i@dWE_Msg.json>}'
    - ^define click '/dwe help'
    - ^define button '"text":"%text%","clickEvent":{"action":"run_command","value":"%click%"},"hoverEvent":{"action":"show_item","value":"%hover%"}'
    - ^define spacer '"text":"  "'
    - ^define msg '"text":"%1%"'
    - ^execute as_server 'tellraw <player.name> {"text":"","extra":[{%button%},{%spacer%},{%msg%}]}'
#    - ^narrate "<&4>[<&6>dWE<&4>]  %1%"

  set_Pos:
    - run s@dWE_Msg 'def:<&f>POS%pos%<&co>  <&3><&o><def[pos%pos%].replace[,].with[<&7>, <&3><&o>]>  <&7>(<&o><proc[selectionSize].context[%pos1%|%pos2%]><&7>)'

  set_pos1:
    - run s@dWE_Msg 'def:<&f>Selection started at <&3><&o><def[cuboid].min.simple.replace[,].with[<&7>,<&3><&o>]>'

  set_pos2:
    - run s@dWE_Msg 'def:<&f>Selection expanded to include<&co> <&3><&o><context.location.simple.replace[,].with[<&7>,<&3><&o>]> <&7>(<&o><proc[selectionSize].context[%pos1%|%pos2%]><&7>)'

  shift:
    - run s@dWE_Msg 'def:<&f>Shifted<&f>  <&7><&o>%direction% <&7>by <&o>%n% <&7>blocks (<&o><proc[selectionSize].context[%pos1%|%pos2%]><&7>)'

  expand:
    - define expandBy '<proc[selectionSize].context[%pos1%|%pos2%].sub[%oldSize%].as_int>'
    - run s@dWE_Msg 'def:<&f>Expanded<&f>  <&7><&o>%direction% <&7>by <&o>%expandBy% <&7>blocks (<&o><proc[selectionSize].context[%pos1%|%pos2%]><&7>)'

  contract:
    - define contractBy '<def[oldSize].sub[<proc[selectionSize].context[%pos1%|%pos2%]>].as_int>'
    - run s@dWE_Msg 'def:<&f>Contracted<&f>  <&7><&o>%direction% <&7>by <&o>%contractBy% <&7>blocks (<&o><proc[selectionSize].context[%pos1%|%pos2%]><&7>)'

  inset:
    - define insetBy '<def[oldSize].sub[<proc[selectionSize].context[%pos1%|%pos2%]>].as_int>'
    - run s@dWE_Msg 'def:<&f>Inset<&f>  <&7>by <&o>%insetBy% <&7>blocks (<&o><proc[selectionSize].context[%pos1%|%pos2%]><&7>)'

  outset:
    - define outsetBy '<proc[selectionSize].context[%pos1%|%pos2%].sub[%oldSize%].as_int>'
    - run s@dWE_Msg 'def:<&f>Outset<&f>  <&7>by <&o>%outsetBy% <&7>blocks (<&o><proc[selectionSize].context[%pos1%|%pos2%]><&7>)'
#
#--------------------------------------
#
#  dWorldEditor Message Constructors
#
# Constructors for the box messages
#
  msgsHeader:
    - narrate "<&5>|----------------------------------------|"
    - narrate "<&5>|<&sp><&sp><&sp><&6>dWorldEditor   <&7><def[msgsHeaderTitle]||>"
    - narrate "<&5>|<&f>"

  msgsFooter:
    - define anthony '"text":"<&7>|Anthony|","clickEvent":{"action":"open_url","value":"http://mineconomy.org"},"hoverEvent":{"action":"show_item","value":"{<i@dwe_Author_Anthony.json>}"}'
    - define morphan '"text":"<&7>Morphan1","clickEvent":{"action":"open_url","value":"http://en.wikipedia.org/wiki/Candy"},"hoverEvent":{"action":"show_item","value":"{<i@dwe_Author_Morphan1.json>}"}'
    - define mcmonkey '"text":"<&7>mcmonkey","clickEvent":{"action":"open_url","value":"http://mcmonkey.org"},"hoverEvent":{"action":"show_item","value":"{<i@dwe_Author_mcmonkey.json>}"}'
    - define spacer '"text":"  "'
    - define prefix '"text":"<&5>|  <&f>Authors<&co>  "'
    - if <player.is_player||false> {
      - execute as_server 'tellraw <player.name> {"text":"","extra":[{%prefix%},{%anthony%},{%spacer%},{%morphan%},{%spacer%},{%mcmonkey%}]}'
      }
      else {
      - announce to_console "<&5>|  <&f>Authors:  <&7>|Anthony|  <&7>Morphan1  <&7>mcmonkey"
      }
    - narrate "<&5>|-----------<&d>S<&5>-<&d>c<&5>-<&d>r<&5>-<&d>o<&5>-<&d>l<&5>-<&d>l<&5>---<&d>U<&5>-<&d>p<&5>-------------|"

  msgsHelp:
    # Filter the list of commands this player can use
    - define commands '<s@dWE_Commands.list_keys[commandArgs].alphanumeric.to_lowercase||li@>'
    - foreach %commands% {
      - if !<proc[dWE_HasPerm_Command].context[%value%|<player||server>]> {
        - define commands '<def[commands].exclude[%value%]>'
        }
        else {
        - foreach next
        }
      }

    # Display the list
    - define page '<def[arg].replace[-].escaped||1>'
    - if <def[page].is[MATCHES].to[number].not> {
      - define page '1'
      }
    - define pages '<def[commands].size.div[5].round_up||1>'
    - if <def[page].is[MORE].than[%pages%]> {
      - define page '%pages%'
      }
    - define highNumber '<def[page].mul[5].as_int>'
    - define lowNumber '<def[highNumber].sub[4].as_int>'
    - define msgsHeaderTitle '<&e>Admin Help   <el@val[<&7><&o>Page <&f><&o>%page% <&7><&o>of %pages%].pad_left[30]>'
    - inject locally msgsHeader instantly
    - foreach <def[commands].get[%lowNumber%].to[%highNumber%]||li@> {
      - narrate "<&5>|<&sp><&sp><&f><def[value].to_titlecase><&co>"
      - narrate "<&5>|<&sp><&sp><&sp><&sp><&7><parse:<s@dWE_Commands.yaml_key[commandArgs.%value%.usage]||No usage info available!>>"
      }
    - narrate "<&5>|"

  msgsAbout:
    - define msgsHeaderTitle 'User Help'
    - inject locally msgsHeader instantly
    - define par1 ""
    - define par2 ""
    - define par3 ""
    - define strings "<el@val[%par1%|%par2%|%par3%].as_list>"
    - foreach <def[strings]> {
      - define string "<&f><&sp><def[value]>"
      - define lines '<proc[dRegions_LineWrap].context[<def[string]>|44]>'
      - foreach <def[lines]> {
        - narrate "<&5>|<&f><&sp><&sp><def[value].unescaped>"
        }
      - narrate "<&5>|"
      }
#
#  END dWorldEditor Messages
#--------------------------------------
#
#
################################################################################
#
#  dWorldEditor Wand Item
#
# This is the house for the wand item
#
#
#--------------------------------------
#
dWE_Wand:
  type: item
  debug: false
  material: m@wood_axe
  display name: dWE Region Selector
  lore:
  - <&7><&o>Left<&7> click to start a selection
  - <&7><&o>Right<&7> click to expand the selection

  script:
    - if !<player.has_permission[dwe.wand]||<player.is_op>> {
      - take i@dWE_Wand
      - run s@dWE_Msg 'def:<&4><&l>ERROR<&co> <&a>You should not have this!' instantly
      - queue clear
      }
    - if <c.click_type.is[==].to[LEFT_CLICK_BLOCK]> {
      - define pos 1
      }
      else if <c.click_type.is[==].to[RIGHT_CLICK_BLOCK]> {
      - define pos 2
      }
      else {
      - queue clear
      }
    - define pos1 '<player.flag[dWEWand].as_list.get[1]||null>'
    - define pos2 '<player.flag[dWEWand].as_list.get[2]||null>'
    - define type '<player.flag[dWEWand_Type]||cuboid>'
    - if <li@cuboid|extender.contains[%type%]||false> {
      - inject locally %type%
      }
      else {
      - inject locally cuboid
      }

  cuboid:
    - if <context.location.simple.is[==].to[<def[pos%pos%]>]||false> {
      - run s@dWE_Msg 'def:<&c>pos%pos% is already set'
      - queue clear
      }
    - define pos%pos% '<context.location.simple>'
    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%
    - inject s@dWE_Msg p:set_Pos

  extender:
    - define cuboid cu@%pos1%|%pos2%
    - if %pos% == 1 {
      - define cuboid cu@<context.location.simple>|<context.location.simple>
      }
      else {
      - if %pos1% == null {
        - run s@DWE_Msg 'Def:<&c>You must begin a selection before you can expand it.'
        - queue clear
        }
      - define cuboid_old %cuboid%
      - define cuboid <def[cuboid].include[<context.location.simple>]>
      - if <def[cuboid_old]> == <def[cuboid]> {
        - run s@dWE_Msg 'def:<&c>That position is already included.'
        - queue clear
        }
      }
    - define pos1 '<def[cuboid].min>'
    - define pos2 '<def[cuboid].max>'
    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%
    - inject s@dWE_Msg p:set_pos%pos%
#
#  END dWorldEditor Wand Item
#--------------------------------------
#
#
################################################################################
#
#  Denizen World Editor Command Script Containers
#
# Every command has its own script container. The root command functions mostly
# as a registrar for commands and provides some initial processing.
#
#
#--------------------------------------
#
#  dWE Command Script Registrar
#
# All commands are registered here and can be run as an argument to /dwe
#
dWE_Commands:
  type: command
  debug: false
  name: dwe
  description: Denizen World Editor
  usage: /dwe
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  commandArgs:
    help:
      usage: '/dwe help <&lt>arg/<&ns><&gt>'
      permissions: []
    about:
      usage: '/dwe about'
      permissions: []
    wand:
      usage: '/dwe wand'
      permissions:
        - wand
    expand:
      usage: '/dwe expand'
      permissions:
        - expand
    contract:
      usage: '/dwe contract'
      permissions:
        - contract
    inset:
      usage: '/dwe inset'
      permissions:
        - inset
    outset:
      usage: '/dwe outset'
      permissions:
        - outset
    shift:
      usage: '/dwe shift'
      permissions:
        - shift
    pos1:
      usage: '/dwe pos1'
      permissions:
        - pos1
    pos2:
      usage: '/dwe pos2'
      permissions:
        - pos2
    hpos1:
      usage: '/dwe hpos1'
      permissions:
        - hpos1
    hpos2:
      usage: '/dwe hpos2'
      permissions:
        - hpos2
    sphere:
      usage: '/dwe sphere'
      permissions:
        - sphere
    chunk:
      usage: '/dwe chunk'
      permissions:
        - chunk
    show:
      usage: '/dwe show'
      permissions:
        - show
    set:
      usage: '/dwe set'
      permissions:
        - set
    undo:
      usage: '/dwe undo'
      permissions:
        - undo

  script:
    - define command '<c.args.get[1].escaped||help>'
    - define comands '<script.list_keys[commandArgs].alphanumeric.to_lowercase||li@>'
    - define args '<c.args.get[2].to[<c.args.size>]||li@>'
    - if !<def[comands].contains[%command%]> || !<proc[dWE_HasPerm_Command].context[%command%|<player||server>]> {
      - inject locally help
      - queue clear
      }
    - inject s@dWE_Commands_%command%

  prerunA:
    - if <c.server||false> {
      - run s@dWE_Msg 'def:<&c>This command must be run as a player.'
      - queue clear
      }
    - if <c.alias> == '.%command%' {
      - if !<proc[dWE_HasPerm_Command].context[%command%|<player>]> {
        - inject s@dWE_Msg p:msgsHelp
        - inject s@dWE_Msg p:msgsFooter
        - queue clear
        }
      - define args "<c.args>"
      }

  prerunB:
    - if <c.server||false> {
      - run s@dWE_Msg 'def:<&c>This command must be run as a player.'
      - queue clear
      }
    - if <c.alias> == '.%command%' {
      - if !<proc[dWE_HasPerm_Command].context[%command%|<player>]> {
        - inject s@dWE_Msg p:msgsHelp
        - inject s@dWE_Msg p:msgsFooter
        - queue clear
        }
      - define args "<c.args>"
      }

    - define pos1 '<player.flag[dWEWand].get[1].as_location||null>'
    - define pos2 '<player.flag[dWEWand].get[2].as_location||null>'
    - if <def[pos1].is[==].to[null]>
      || <def[pos2].is[==].to[null]> {
      - run s@dWE_Msg 'def:<&c>No region selected!'
      - queue clear
      }
    - define oldSize '<proc[selectionSize].context[%pos1%|%pos2%]>'

#
#  END dWE Command Registrar
#--------------------------------------
#
#
################################################################################
#
#
#  dWorldEditor Command Scripts
#
# This section holds all of the commands for dWorldEditor. The root command /dwe
# takes many arguments but most of those arguments can also be run as individual
# commands. This results in dWorldEditor command args having a shorthand version
# in the form of /.arg
#

dWE_Commands_Help:
  type: command
  debug: false
  speed: 0
  name: .help
  description: Show dWorldEditor help
#  permission: my.permission.node
#  permission message:
  usage: /.help
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
    - if !<def[command].exists> {
      - define command 'help'
      }
    - inject s@dWE_Commands p:prerunA

    - define arg '<def[args].get[1].escaped||null>'
    - if <def[comands].contains[%arg%]||false> {
      - run s@dWE_Msg 'def:<parse:<script.yaml_key[commandArgs.%arg%.description]||A not so useless description should be here!>>'
      - run s@dWE_Msg 'def:<parse:<script.yaml_key[commandArgs.%arg%.usage]||No help for you!>>'
      - queue clear
      }
    - inject s@dWE_Msg p:msgsHelp
    - inject s@dWE_Msg p:msgsFooter
    - queue clear

dWE_Commands_About:
  type: command
  debug: false
  speed: 0
  name: .about
  description: Details about the project
#  permission: my.permission.node
#  permission message:
  usage: /.about
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
    - if !<def[command].exists> {
      - define command 'about'
      }
    - inject s@dWE_Commands p:prerunA

    - inject s@dWE_Msg p:msgsAbout
    - inject s@dWE_Msg p:msgsFooter
    - queue clear


dWE_Commands_Show:
  type: command
  debug: false
  speed: 0
  name: .show
  description: Show your current dWE selection
#  permission: my.permission.node
#  permission message:
  usage: /.show
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
    - if !<def[command].exists> {
      - define command 'show'
      }
    - inject s@dWE_Commands p:prerunB
    - showfake <player.flag[dWE.show].split[/].get[1]||m@blue_stained_glass> <cu@%pos1%|%pos2%.outline> to:<player> d:<player.flag[dWE.show].split[/].get[2]||10>s

dWE_Commands_Wand:
  type: command
  debug: false
  speed: 0
  name: .wand
  description: Show your current dWE selection
#  permission: my.permission.node
#  permission message:
  usage: /.wand (-type [cuboid/extender])
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
    - if !<def[command].exists> {
      - define command 'wand'
      }
    - inject s@dWE_Commands p:prerunA

    - if <def[args].find[-type].is[OR_MORE].than[0]||false> {
      - define type <def[args].get[<def[args].find[-type].add[1].as_int>]||null>
      - if <li@cuboid|extender.contains[%type%]||false> {
        - run s@dWE_Msg 'def:<&7><&o>Now using the <&f><&o>%type% <&7><&o>selection wand'
        - flag player 'dWEWand_Type:%type%'
        }
        else {
        - run s@dWE_Msg 'def:<&4><&l>ERROR<&co> <&c>Unknown wand type!'
        - run s@dWE_Msg 'def:<&c>SYNTAX<&co> <&e>/dwe -type <&6><&lb><&7>cuboid/extender<&6><&rb>'
        }
      - queue clear
      }
    - if <player.inventory.find_imperfect[<i@dWE_Wand>]> != '-1' {
      - run s@dWE_Msg 'def:<&c>You already have a wand!'
      }
      else {
      - give i@dWE_Wand
      }
    - define type '<player.flag[dWEWand_Type]||cuboid>'
    - choose '%type%':
      - case 'cuboid':
        - run s@dWE_Msg 'def:<&a>Left click to select pos1'
        - run s@dWE_Msg 'def:<&a>Right click to select pos2'
      - case 'extender':
        - run s@dWE_Msg 'def:<&a>Left click to start a selection,'
        - run s@dWE_Msg 'def:<&a>Right click to expand the selection.'
      - default:
        - run s@dWE_Msg 'def:<&4><&l>ERROR<&co> <&c>Unknown wand type!'
        - run s@dWE_Msg 'def:<&c>SYNTAX<&co> <&e>/dwe -type <&6><&lb><&7>cuboid/extender<&6><&rb>'
    - queue clear

dWE_Commands_Pos1:
  type: command
  debug: false
  speed: 0
  name: .pos1
  description: Set your current position as position 1
#  permission: my.permission.node
#  permission message:
  usage: /.pos1
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
    - if !<def[command].exists> {
      - define command 'pos1'
      }
    - inject s@dWE_Commands p:prerunA

    - define pos1 '<player.flag[dWEWand].as_list.get[1]||null>'
    - if <player.location.simple.is[==].to[%pos1%]> {
      - run s@dWE_Msg 'def:<&c>pos1 is already set'
      - queue clear
      }
    - define pos1 '<player.location.simple>'
    - define pos2 '<player.flag[dWEWand].as_list.get[2]||null>'
    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%
    - define pos 1
    - inject s@dWE_Msg p:set_Pos

dWE_Commands_Pos2:
  type: command
  debug: false
  speed: 0
  name: .pos2
  description: Set your current position as position 2
#  permission: my.permission.node
#  permission message:
  usage: /.pos2
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
    - if !<def[command].exists> {
      - define command 'pos2'
      }
    - inject s@dWE_Commands p:prerunA

    - define pos2 '<player.flag[dWEWand].as_list.get[2]||null>'
    - if <player.location.simple.is[==].to[%pos2%]> {
      - run s@dWE_Msg 'def:<&c>pos2 is already set'
      - queue clear
      }
    - define pos1 '<player.flag[dWEWand].as_list.get[1]||null>'
    - define pos2 '<player.location.simple>'
    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%
    - define pos 2
    - inject s@dWE_Msg p:set_Pos

dWE_Commands_Hpos1:
  type: command
  debug: false
  speed: 0
  name: .hpos1
  description: Set your crosshair position as position 1
#  permission: my.permission.node
#  permission message:
  usage: /.hpos1
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
    - if !<def[command].exists> {
      - define command 'hpos1'
      }
    - inject s@dWE_Commands p:prerunA

    - define pos1 '<player.flag[dWEWand].as_list.get[1]||null>'
    - if <player.location.cursor_on[160].simple.is[==].to[%pos1%]> {
      - run s@dWE_Msg 'def:<&c>pos1 is already set'
      - queue clear
      }
    - define pos1 '<player.location.cursor_on[160].simple>'
    - define pos2 '<player.flag[dWEWand].as_list.get[2]||null>'
    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%
    - define pos 1
    - inject s@dWE_Msg p:set_Pos

dWE_Commands_Hpos2:
  type: command
  debug: false
  speed: 0
  name: .hpos2
  description: Set your crosshair position as position 2
#  permission: my.permission.node
#  permission message:
  usage: /.hpos2
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
    - if !<def[command].exists> {
      - define command 'hpos2'
      }
    - inject s@dWE_Commands p:prerunA

    - define pos2 '<player.flag[dWEWand].as_list.get[2]||null>'
    - if <player.location.cursor_on[160].simple.is[==].to[%pos2%]> {
      - run s@dWE_Msg 'def:<&c>pos2 is already set'
      - queue clear
      }
    - define pos1 '<player.flag[dWEWand].as_list.get[1]||null>'
    - define pos2 '<player.location.cursor_on[160].simple>'
    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%
    - define pos 2
    - inject s@dWE_Msg p:set_Pos

dWE_Commands_Chunk:
  type: command
  debug: false
  speed: 0
  name: .chunk
  description: Selects the chunk you are standing in
#  permission: my.permission.node
#  permission message:
  usage: /.chunk
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
    - if !<def[command].exists> {
      - define command 'chunk'
      }
    - inject s@dWE_Commands p:prerunA

    - define pos1 '<player.location.get_chunk.cuboid.min>'
    - define pos2 '<player.location.get_chunk.cuboid.max>'
    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%
    - run s@dWE_Msg 'def:<&f>Chunk selected<&co> <player.location.get_chunk.replace[ch@].split[,].get[1|2].comma_separated>'

dWE_Commands_Expand:
  type: command
  debug: false
  speed: 0
  name: .expand
  description: Expands your selection
#  permission: my.permission.node
#  permission message:
  usage: /.expand
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
  # /dwe expand <amount/v> (n/s/e/w/u/d)
    - if !<def[command].exists> {
      - define command 'expand'
      }
    - if <c.server||false> {
      - run s@dWE_Msg 'def:<&c>This command must be run as a player.'
      - queue clear
      }
    - inject s@dWE_Commands p:prerunB

    - if <def[args].get[1].escaped.is[==].to[v]||false> {
      - define pos1 '<def[pos1].sub[0,<def[pos1].y>,0].replace[l@]>'
      - define pos2 '<def[pos2].add[0,<el@val[255].sub[<def[pos2].y>]>,0].replace[l@]>'
#      - define pos1 '<def[pos1].x>,0,<def[pos1].z>'
#      - define pos2 '<def[pos2].x>,255,<def[pos2].z>'
      - flag player dWEWand:!
      - flag player dWEWand:|:%pos1%|%pos2%
      - define direction 'Vertically'
      - inject s@dWE_Msg p:expand
      - queue clear
      }

    - define n '<def[args].get[1].escaped.round_up||0>'
    - define d '<def[args].get[2].escaped||0>'
    - if <def[n].is[OR_LESS].than[0]||true> {
      - run s@dWE_Msg 'def:<&c>SYNTAX<&co> <&e>/dwe expand <&6><&lb><&7>amount/v<&6><&rb> <&6>(<&7>direction<&6>)'
      - queue clear
      }
    - if <el@[li@n|e|s|w|u|d].contains[%d%].not||true> {
      - define d '<proc[getFacing].context[<player.location.pitch.as_money>|<player.location.yaw.as_money>]>'
      }
    - if <el@[li@n|e|s|w|u|d].contains[%d%].not||true> {
      - run s@dWE_Msg 'def:<&4><&l>ERROR<&co> <&c>Unknown direction!'
      - queue clear
      }

    - choose '%d%':
      - case 'n':
        - if <def[pos1].z.is[OR_LESS].than[<def[pos2].z>]> {
          - define pos1 '<def[pos1].sub[0,0,%n%]>'
          }
          else {
          - define pos2 '<def[pos2].sub[0,0,%n%]>'
          }
        - define direction 'North'
        - inject s@dWE_Msg p:expand

      - case 'e':
        - if <def[pos1].x.is[OR_MORE].than[<def[pos2].x>]> {
          - define pos1 '<def[pos1].add[%n%,0,0]>'
          }
          else {
          - define pos2 '<def[pos2].add[%n%,0,0]>'
          }
        - define direction 'East'
        - inject s@dWE_Msg p:expand

      - case 's':
        - if <def[pos1].z.is[OR_MORE].than[<def[pos2].z>]> {
          - define pos1 '<def[pos1].add[0,0,%n%]>'
          }
          else {
          - define pos2 '<def[pos2].add[0,0,%n%]>'
          }
        - define direction 'South'
        - inject s@dWE_Msg p:expand

      - case 'w':
        - if <def[pos1].x.is[OR_LESS].than[<def[pos2].x>]> {
          - define pos1 '<def[pos1].sub[%n%,0,0]>'
          }
          else {
          - define pos2 '<def[pos2].sub[%n%,0,0]>'
          }
        - define direction 'West'
        - inject s@dWE_Msg p:expand

      - case 'u':
        - if <def[pos1].y.is[OR_MORE].than[<def[pos2].y>]> {
          - define pos1 '<def[pos1].add[0,%n%,0]>'
          }
          else {
          - define pos2 '<def[pos2].add[0,%n%,0]>'
          }
        - define direction 'Up'
        - inject s@dWE_Msg p:expand

      - case 'd':
        - if <def[pos1].y.is[OR_LESS].than[<def[pos2].y>]> {
          - define pos1 '<def[pos1].sub[0,%n%,0]>'
          }
          else {
          - define pos2 '<def[pos2].sub[0,%n%,0]>'
          }
        - define direction 'Down'
        - inject s@dWE_Msg p:expand

      - default:
        - run s@dWE_Msg 'def:<&4><&l>ERROR<&co> <&c>An impossible error occurred while expanding your selection!'
        - queue clear
    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%

dWE_Commands_Contract:
  type: command
  debug: false
  speed: 0
  name: .contract
  description: Contracts your selection
#  permission: my.permission.node
#  permission message:
  usage: /.contract
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
  # /dwe contract <amount/v> (n/s/e/w/u/d)
    - if !<def[command].exists> {
      - define command 'contract'
      }
    - inject s@dWE_Commands p:prerunB

    - define n '<def[args].get[1].escaped.round_up||0>'
    - define d '<def[args].get[2].escaped||0>'
    - if <def[n].is[OR_LESS].than[0]||true> {
      - run s@dWE_Msg 'def:<&c>SYNTAX<&co> <&e>/dwe contract <&6><&lb><&7>amount<&6><&rb> <&6>(<&7>direction<&6>)'
      - queue clear
      }
    - if <el@[li@n|e|s|w|u|d].contains[%d%].not||true> {
      - define d '<proc[getFacing].context[<player.location.pitch.as_money>|<player.location.yaw.as_money>]>'
      }
    - if <el@[li@n|e|s|w|u|d].contains[%d%].not||true> {
      - run s@dWE_Msg 'def:<&4><&l>ERROR<&co> <&c>Unknown direction!'
      - queue clear
      }

    - choose '%d%':
      - case 'n':
        - if <def[pos1].z.is[OR_LESS].than[<def[pos2].z>]> {
          - define pos2 '<def[pos2].sub[0,0,%n%]>'
          }
          else {
          - define pos1 '<def[pos1].sub[0,0,%n%]>'
          }
        - define direction 'North'
        - inject s@dWE_Msg p:contract

      - case 'e':
        - if <def[pos1].x.is[OR_MORE].than[<def[pos2].x>]> {
          - define pos2 '<def[pos2].add[%n%,0,0]>'
          }
          else {
          - define pos1 '<def[pos1].add[%n%,0,0]>'
          }
        - define direction 'East'
        - inject s@dWE_Msg p:contract

      - case 's':
        - if <def[pos1].z.is[OR_MORE].than[<def[pos2].z>]> {
          - define pos2 '<def[pos2].add[0,0,%n%]>'
          }
          else {
          - define pos1 '<def[pos1].add[0,0,%n%]>'
          }
        - define direction 'South'
        - inject s@dWE_Msg p:contract

      - case 'w':
        - if <def[pos1].x.is[OR_LESS].than[<def[pos2].x>]> {
          - define pos2 '<def[pos2].sub[%n%,0,0]>'
          }
          else {
          - define pos1 '<def[pos1].sub[%n%,0,0]>'
          }
        - define direction 'West'
        - inject s@dWE_Msg p:contract

      - case 'u':
        - if <def[pos1].y.is[OR_MORE].than[<def[pos2].y>]> {
          - define pos2 '<def[pos2].add[0,%n%,0]>'
          }
          else {
          - define pos1 '<def[pos1].add[0,%n%,0]>'
          }
        - define direction 'Up'
        - inject s@dWE_Msg p:contract

      - case 'd':
        - if <def[pos1].y.is[OR_LESS].than[<def[pos2].y>]> {
          - define pos2 '<def[pos2].sub[0,%n%,0]>'
          }
          else {
          - define pos1 '<def[pos1].sub[0,%n%,0]>'
          }
        - define direction 'Down'
        - inject s@dWE_Msg p:contract

      - default:
        - run s@dWE_Msg 'def:<&4><&l>ERROR<&co> <&c>An impossible error occurred while contracting your selection!'
        - queue clear

    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%

dWE_Commands_Inset:
  type: command
  debug: false
  speed: 0
  name: .inset
  description: Insets your selection
#  permission: my.permission.node
#  permission message:
  usage: /.inset
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
  # /dwe inset <number> -h
    - if !<def[command].exists> {
      - define command 'inset'
      }
    - inject s@dWE_Commands p:prerunB

    - define n '<def[args].filter[is[matches].to[number]].get[1]||0>'
    - if <def[n].is[OR_LESS].than[0]||true> {
      - run s@dWE_Msg 'def:<&c>SYNTAX<&co> <&e>/dwe inset <&6><&lb><&7>amount<&6><&rb> <&6>(<&7>-h<&6>)'
      - queue clear
      }
    - define cuboid cu@%pos1%|%pos2%
    - if '<def[args].contains[-h].not||false>' {
      - define cuboid 'cu@<def[cuboid].min.add[0,%n%,0]>|<def[cuboid].max.sub[0,%n%,0]>'
      }
    - define cuboid 'cu@<def[cuboid].min.add[%n%,0,%n%]>|<def[cuboid].max.sub[%n%,0,%n%]>'
    - define pos1 '<def[cuboid].min>'
    - define pos2 '<def[cuboid].max>'
    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%
    - inject s@dWE_Msg p:inset

dWE_Commands_Outset:
  type: command
  debug: false
  speed: 0
  name: .outset
  description: Outsets your selection
#  permission: my.permission.node
#  permission message:
  usage: /.outset
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
  # /dwe outset <number> -h
    - if !<def[command].exists> {
      - define command 'outset'
      }
    - inject s@dWE_Commands p:prerunB

    - define n '<def[args].filter[is[matches].to[number]].get[1]||0>'
    - if <def[n].is[OR_LESS].than[0]||true> {
      - run s@dWE_Msg 'def:<&c>SYNTAX<&co> <&e>/dwe outset <&6><&lb><&7>amount<&6><&rb> <&6>(<&7>-h<&6>)'
      - queue clear
      }
    - define cuboid cu@%pos1%|%pos2%
    - if '<def[args].contains[-h].not||false>' {
      - define cuboid 'cu@<def[cuboid].min.sub[0,%n%,0]>|<def[cuboid].max.add[0,%n%,0]>'
      }
    - define cuboid 'cu@<def[cuboid].min.sub[%n%,0,%n%]>|<def[cuboid].max.add[%n%,0,%n%]>'
    - define pos1 '<def[cuboid].min>'
    - define pos2 '<def[cuboid].max>'
    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%
    - inject s@dWE_Msg p:outset

dWE_Commands_Shift:
  type: command
  debug: false
  speed: 0
  name: .shift
  description: Shifts your selection
#  permission: my.permission.node
#  permission message:
  usage: /.shift
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>
  math:
    n: '0,0,-<def[n]>'
    s: '0,0,<def[n]>'
    w: '-<def[n]>,0,0'
    e: '<def[n]>,0,0'
    d: '0,-<def[n]>,0'
    u: '0,<def[n]>,0'
  d:
    n: 'North'
    s: 'South'
    w: 'West'
    e: 'East'
    d: 'Down'
    u: 'Up'

  script:
  # /dwe shift <amount> (n/s/e/w/u/d)
    - if !<def[command].exists> {
      - define command 'shift'
      }
    - inject s@dWE_Commands p:prerunB

    - define n '<def[args].get[1].escaped.round_up||0>'
    - define d '<def[args].get[2].escaped||0>'
    - if <def[n].is[OR_LESS].than[0]||true> {
      - run s@dWE_Msg 'def:<&c>SYNTAX<&co> <&e>/dwe shift <&6><&lb><&7>amount<&6><&rb> <&6>(<&7>direction<&6>)'
      - queue clear
      }
    - if <el@[li@n|e|s|w|u|d].contains[%d%].not||true> {
      - define d '<proc[getFacing].context[<player.location.pitch.as_money>|<player.location.yaw.as_money>]>'
      }
    - if <el@[li@n|e|s|w|u|d].contains[%d%].not||true> {
      - run s@dWE_Msg 'def:<&4><&l>ERROR<&co> <&c>Unknown direction!'
      - queue clear
      }
    - define direction '<script.yaml_key[d.%d%]>'
    - repeat 2 {
      - define pos%value% '<def[pos%value%].add[<parse:<script.yaml_key[math.%d%]>>]>'
      }
    - inject s@dWE_Msg p:shift
    - flag player dWEWand:!
    - flag player dWEWand:|:%pos1%|%pos2%

dWE_Commands_Set:
  type: command
  debug: false
  speed: 0
  name: .Set
  description: Set your selection to a material
#  permission: my.permission.node
#  permission message:
  usage: /.set
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
  # /dwe set <material>
    - if !<def[command].exists> {
      - define command 'set'
      }
    - inject s@dWE_Commands p:prerunB

    - define material <def[args].get[1]||null>
    - if %material% !matches material {
      - run s@dWE_Msg 'def:<&c>Must specify a valid material.'
      - queue clear
      }
    - define blocks <cu@%pos1%|%pos2%.blocks>
    - define undoMaterials '<def[blocks].parse[material.full]>'
    - flag player 'dWE.UndoHistory:->:set_blocks;cu@%pos1%|%pos2%;%undoMaterials%'
    - ~modifyblock %blocks% %material% delayed
#    - ~modifyblock cu@%pos1%|%pos2% '%material%' delayed
    - run s@dWE_Msg 'def:Set complete!'

dWE_Commands_Sphere:
  type: command
  debug: false
  speed: 0
  name: .sphere
  description: Creates an ellipsoid based on wand selection
#  permission: my.permission.node
#  permission message:
  usage: /.sphere
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
  # /dwe sphere <x>,[<y>,<z>] <material>
    - if !<def[command].exists> {
      - define command 'sphere'
      }
    - inject s@dWE_Commands p:prerunB

    - if <def[args].size> == 0 || ( <def[args].size> == 1 && <def[args].get[1]> !matches material ) {
      - run s@dWE_Msg 'def:<&c>/dwe sphere <&lt>x<&gt>,[<&lt>y<&gt>,<&lt>z<&gt>] <&lt>material<&gt>'
      - queue clear
      }

    - define cuboid 'cu@%pos1%|%pos2%'
    - define center '<def[cuboid].center.simple>'
    - if <def[args].size> == 1 {
    # Only specify material. Sizes are determined by wand selection.
      - define material '<def[args].get[1]>'
      - define cSize '<def[cuboid].size.add[1,1,1].div[2].simple.split_by[,]>'
      - define rads "<def[cSize].get[1]>,<def[cSize].get[2]>,<def[cSize].get[3]>"
      - narrate "%rads%"
      }
      else {
      - define rads '<def[args].get[1].split_by[,]>'
      - if <def[rads].size> == 2 || <def[rads].filter[is[matches].to[number]].is[!=].to[%rads%]> {
        - run s@dWE_Msg 'def:<&c>/dwe sphere <&lt>x<&gt>,[<&lt>y<&gt>,<&lt>z<&gt>] <&lt>material<&gt>'
        - queue clear
        }
      - define x '<def[rads].get[1].abs>'
      - define y '<def[rads].get[2].abs||%x%>'
      - define z '<def[rads].get[3].abs||%x%>'
      - define rads "%x%,%y%,%z%"
      - define material '<def[args].get[2]>'
      }

    - define ellipsoid 'ellipsoid@%center%,%rads%'
    - define blocks '<def[ellipsoid].blocks>'
    - define undoMaterials '<def[blocks].parse[material.full]>'
    - flag player 'dWE.UndoHistory:->:set_blocks;<def[ellipsoid]>;%undoMaterials%'
    - ~modifyblock %blocks% %material% delayed
#    - ~modifyblock %ellipsoid% %material% delayed
    - run s@dWE_Msg 'def:Set complete!'

dWE_Commands_Copy:
  type: command
  debug: false
  speed: 0
  name: .copy
  description: Copy your selection
#  permission: my.permission.node
#  permission message:
  usage: /.copy
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
  # /dwe copy <number> -h
    - if !<def[command].exists> {
      - define command 'copy'
      }
    - inject s@dWE_Commands p:prerunB

    - define blocks '<cu@%pos1%|%pos2%.blocks>'
    - flag player dWE.Clipboard:!
    - flag player 'dWE.Clipboard:|:<def[blocks].parse[material]>'
    - run s@dWE_Msg 'def:<&7><&o>Selection saved to clipboard'

dWE_Commands_Paste:
  type: command
  debug: false
  speed: 0
  name: .paste
  description: Paste your selection
#  permission: my.permission.node
#  permission message:
  usage: /.paste
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
  # /dwe paste <number> -h
    - if !<def[command].exists> {
      - define command 'paste'
      }
    - inject s@dWE_Commands p:prerunB
    - run s@dWE_Msg 'def:<&c>This command doesn<&sq>t do anything yet!'

dWE_Commands_Rotate:
  type: command
  debug: false
  speed: 0
  name: .rotate
  description: Rotate your selection
#  permission: my.permission.node
#  permission message:
  usage: /.rotate
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
  # /dwe rotate <number> -h
    - if !<def[command].exists> {
      - define command 'rotate'
      }
    - inject s@dWE_Commands p:prerunB
    - run s@dWE_Msg 'def:<&c>This command doesn<&sq>t do anything yet!'

dWE_Commands_Undo:
  type: command
  debug: false
  speed: 0
  name: .undo
  description: Undo edits
#  permission: my.permission.node
#  permission message:
  usage: /.undo
  aliases: ''
  allowed help:
  - determine <player.has_permission[dwe.basic]||<player.is_op>>

  script:
  # /dwe undo <number> -h
    - if !<def[command].exists> {
      - define command 'undo'
      }
    - inject s@dWE_Commands p:prerunA

    - define last_action '<player.flag[dWE.UndoHistory].as_list.last||null>'
    - if <def[last_action]> == 'null' {
      - run s@dWE_Msg 'def:<&6>Nothing to undo!'
      - queue clear
      }
    - choose '<def[last_action].split[;].get[1]>':
      - case 'set_blocks':
        - define selection '<def[last_action].split[;].get[2]||null>'
        - define materials '<def[last_action].split[;].get[3]||li@>'
        - ~modifyblock %selection% %materials% delayed
        - run s@dWE_Msg 'def:<&a>Undo complete!'
      - 'default':
        - run s@dWE_Msg 'def:<&4>ERROR <&c><def[last_action].split[;].get[1]> is an unknown type!'
        - queue clear
    - flag player 'dWE.UndoHistory:<-:%last_action%'

#
#  END dWE Command Scripts
#--------------------------------------
#
################################################################################
#
#  dWorldEditor Other Utilities
#
# Other Utility functions used throughout dWorldEditor
#
#--------------------------------------
#
#  Command Permission Check Procedure script
#  - Used to check if a player has permission to use a command.
#
dWE_HasPerm_Command:
# Usage: <proc[dWE_HasPerm_Command].context[command|player]>
  type: procedure
  speed: 0
  debug: false
  definitions: command|player

  script:
    - if %player% == 'server' || <def[player].is_op> || <def[player].permission[dwe.admin]> {
      - determine true
      }
    - define perms "<s@dWE_Commands.yaml_key[commandArgs.%command%.permissions]||li@>"
    - if <def[perms].is_empty> {
      - determine true
      }
    - foreach %perms% {
      - define perm '<parse:%value%>'
      - if <def[player].permission[dwe.%perm%]> {
        - determine true
        }
      }
    - determine false
#
#--------------------------------------
#
#  getFacing Procedure script
#  - Returns the cardinal direction the player is facing
#
getFacing:
# Usage: <proc[getFacing].context[%pitch%|%yaw%]>
  type: procedure
  debug: false
  definitions: pitch|yaw
  script:
    - if <def[pitch].is[OR_MORE].than[45]||false> determine d
      else if <def[pitch].is[OR_LESS].than[-45]||false> determine u
      else if <def[yaw].is[OR_MORE].than[337.5]||false> || <def[yaw].is[LESS].than[22.5]||false> determine s
      else if <def[yaw].is[OR_MORE].than[292.5]||false> determine se
      else if <def[yaw].is[OR_MORE].than[247.5]||false> determine e
      else if <def[yaw].is[OR_MORE].than[202.5]||false> determine ne
      else if <def[yaw].is[OR_MORE].than[157.5]||false> determine n
      else if <def[yaw].is[OR_MORE].than[112.5]||false> determine nw
      else if <def[yaw].is[OR_MORE].than[67.5]||false> determine w
      else if <def[yaw].is[OR_MORE].than[22.5]||false> determine sw
      else determine 0
#
#--------------------------------------
#
#  selectionSize Procedure script
#  - Returns the size of the players selection
#
selectionSize:
# Usage: <proc[selectionSize].context[%pos1%|%pos2%]>
  type: procedure
  debug: false
  definitions: pos1|pos2
  script:
    - define x '<def[pos1].as_location.x.sub[<def[pos2].as_location.x||0>].abs.add[1]||1>'
    - define y '<def[pos1].as_location.y.sub[<def[pos2].as_location.y||0>].abs.add[1]||1>'
    - define z '<def[pos1].as_location.z.sub[<def[pos2].as_location.z||0>].abs.add[1]||1>'
    - determine '<def[x].mul[%y%].mul[%z%].as_int||0>'



#
#  END dWE Other Utilities
#--------------------------------------
#
#  dWorldEditor Author Banner Items
#
# Banner items representing the authors
#
dwe_Author_mcmonkey:
  type: item
  debug: false
  material: creeper_skull
  display name: <&2><&l>MCMONKEY
  lore:
  - <&a>Click to visit http://mcmonkey.org

dwe_Author_Morphan1:
  type: item
  debug: false
  material: creeper_skull
  display name: <&2><&l>Morphan1
  lore:
  - <&a>pls2buymesome

dwe_Author_Anthony:
  type: item
  debug: false
  material: i@human_skull
  display name: "<&f>             |Anthony|"
  lore:
  - <&7>  Owner<&co> <&e>M<&6>ine<&e>C<&6>onomy <&e>N<&6>etwork
  - <&5>-------------------------
  - <&7>
  - <&7>  Ive been playing minecraft
  - <&7> and running a server since
  - <&7> 2010. I have fun and share
  - <&7> what I do.
  - <&7>
  - <&9>           Click To Visit
#
#  END dWE Author Banner Items
#--------------------------------------
